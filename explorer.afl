#include_once ".\\Formulas\\Custom\\ds\\lib.afl"

// user parameters
liquidityMinLimit = 1000000;
rsiRange = 14;
zigzagReverse = 12;
fastMAPeriod = 5;
slowMAPeriod = 10;

// filter by liquidity
liquidity = MA(Close * Volume, 21);
Filter = liquidity >= liquidityMinLimit;

// show tickers with max and min price in selected set of bars
bars = 28;
barsPriceExtremum = 0;
barsPriceExtremum = IIf(High >= HHV(High, bars), 1, barsPriceExtremum);
barsPriceExtremum = IIf(Low <= LLV(Low, bars), -1, barsPriceExtremum);
AddColumn(barsPriceExtremum, NumToStr(bars, 1.0) + " Bars Price", 1.0);

// show tickers with max and min volume in selected set of bars
barsVolumeExtremum = 0;
barsVolumeExtremum = IIf(Volume >= HHV(Volume, bars), 1, barsVolumeExtremum);
barsVolumeExtremum = IIf(Volume <= LLV(Volume, bars), -1, barsVolumeExtremum);
AddColumn(barsVolumeExtremum, NumToStr(bars, 1.0) + " Bars Vol", 1.0);

// show tickers with max and min price in all bars
allBarsPriceExtremum = 0;
allBarsPriceExtremum = IIf(High >= Highest(High), 1, allBarsPriceExtremum);
allBarsPriceExtremum = IIf(Low <= Lowest(Low), -1, allBarsPriceExtremum);
AddColumn(allBarsPriceExtremum, "AllTime Price", 1.0);

// show tickers with max and min volume in all bars
allBarsVolumeExtremum = 0;
allBarsVolumeExtremum = IIf(Volume >= Highest(Volume), 1, allBarsVolumeExtremum);
allBarsVolumeExtremum = IIf(Volume <= Lowest(Volume), -1, allBarsVolumeExtremum);
AddColumn(allBarsVolumeExtremum, "AllTime Volume", 1.0);

// show tickers with RSI divergence
indicatorName = "RSI";
indicatorValues = RSI(rsiRange);
indicatorZigZags = Zig(indicatorValues, zigzagReverse);
divrgs = Divrg(Close, indicatorValues, zigzagReverse);
AddColumn(divrgs, indicatorName + " Div", 1.0);

// show tickers with price between fast MA and slow MA
fastMA = MA(Close, fastMAPeriod);
slowMA = MA(Close, slowMAPeriod); 
priceInValueArea = 0;
priceInValueArea = IIf(Close >= slowMA AND Close <= fastMA, 1, priceInValueArea);
priceInValueArea = IIf(Close <= slowMA AND Close >= fastMA, -1, priceInValueArea);
AddColumn(priceInValueArea, "Price Between MA", 1.0);

// show tickers with trend line price breaks
studyNames = "RE,SU,UP,DN,RI,ST,AA,AB,AC,AD,AE";
chartId = 1;
index = 0;
priceBrokeTrendLine = 0;
while((studyName = StrExtract(studyNames, index)) != "")
{
	priceBrokeTrendLine = priceBrokeTrendLine OR TrendLineBreakPrice(studyName, chartId);
	index++;
}
AddColumn(priceBrokeTrendLine, "Price Break Trend", 1.0);