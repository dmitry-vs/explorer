#include_once ".\\Formulas\\Custom\\ds\\lib.afl"

// user parameters
liquidityMinLimit = 1000000;
rsiRange = 14;
zigzagReverse = 12;
fastMAPeriod = 5;
slowMAPeriod = 10;

// filter by liquidity
liquidity = MA(Close * Volume, 21);
Filter = liquidity >= liquidityMinLimit;

// show tickers with max and min price in selected set of bars
bars = 28;
barsPriceExtremum = 0;
barsPriceExtremum = IIf(High >= HHV(High, bars), 1, barsPriceExtremum);
barsPriceExtremum = IIf(Low <= LLV(Low, bars), -1, barsPriceExtremum);
ExplorerAddColumn(NumToStr(bars, 1.0) + " Bars Price", barsPriceExtremum);

// show tickers with max and min volume in selected set of bars
barsVolumeExtremum = 0;
barsVolumeExtremum = IIf(Volume >= HHV(Volume, bars), 1, barsVolumeExtremum);
barsVolumeExtremum = IIf(Volume <= LLV(Volume, bars), -1, barsVolumeExtremum);
ExplorerAddColumn(NumToStr(bars, 1.0) + " Bars Vol", barsVolumeExtremum);

// show tickers with max and min price in all bars
allBarsPriceExtremum = 0;
allBarsPriceExtremum = IIf(High >= Highest(High), 1, allBarsPriceExtremum);
allBarsPriceExtremum = IIf(Low <= Lowest(Low), -1, allBarsPriceExtremum);
ExplorerAddColumn("AllTime Price", allBarsPriceExtremum);

// show tickers with max and min volume in all bars
allBarsVolumeExtremum = 0;
allBarsVolumeExtremum = IIf(Volume >= Highest(Volume), 1, allBarsVolumeExtremum);
allBarsVolumeExtremum = IIf(Volume <= Lowest(Volume), -1, allBarsVolumeExtremum);
ExplorerAddColumn("AllTime Volume", allBarsVolumeExtremum);

// show tickers with RSI divergence
indicatorName = "RSI";
indicatorValues = RSI(rsiRange);
indicatorZigZags = Zig(indicatorValues, zigzagReverse);
divrgs = Divrg(Close, indicatorValues, zigzagReverse);
ExplorerAddColumn(indicatorName + " Div", divrgs);

// show tickers with price between fast MA and slow MA
fastMA = MA(Close, fastMAPeriod);
slowMA = MA(Close, slowMAPeriod); 
priceInValueArea = 0;
priceInValueArea = IIf(Close >= slowMA AND Close <= fastMA, 1, priceInValueArea);
priceInValueArea = IIf(Close <= slowMA AND Close >= fastMA, -1, priceInValueArea);
ExplorerAddColumn("Price Between MA", priceInValueArea);

// show tickers with trend line price breaks
studyNames = "SU,SA,SB,SC,SD,SE,SF,RA,RB,RC,RD,RE,RF,UP,DN,RI,ST";
chartId = 1;
index = 0;
priceBrokeTrendLine = 0;
while((studyName = StrExtract(studyNames, index)) != "")
{
	priceBrokeTrendLine = IIf(TrendLineBreakPrice(studyName, chartId) == 1, 1, priceBrokeTrendLine);
	priceBrokeTrendLine = IIf(TrendLineBreakPrice(studyName, chartId) == -1, -1, priceBrokeTrendLine);
	index++;
}
ExplorerAddColumn("Price Break Trend", priceBrokeTrendLine);
